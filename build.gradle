apply plugin: 'java'
apply plugin: 'fatjar'

group         = 'com.sendgrid'
version       = "0.0.1"
ext.packaging = 'jar'

buildscript {
  dependencies {
    classpath 'eu.appsatori:gradle-fatjar-plugin:0.1.2' // adds fatJar task
  }
  repositories {
    mavenCentral()
  }
}

dependencies {
  compile 'org.json:json:20140107'
  testCompile group:'org.skyscreamer', name:'jsonassert', version:'1.+'
  testCompile group:'junit', name:'junit', version:'4.+'
}

repositories {
  mavenCentral()
}

// adds 'with-dependencies' to the fatJar name
fatJar {
  classifier 'jar'
  baseName "smtpapi"
  manifest {
    attributes("Implementation-Title": "SMTPAPI", "Implementation-Version": version)
  }
}

// copy fatJar to base project directory so they will be in git (and on github for download)
build << {
  copy {
    println "Copying ${fatJar.archiveName} to $projectDir/repo/com/github/sendgrid/$version"
    from("$buildDir/libs/${fatJar.archiveName}")
    into("$projectDir/repo/com/github/sendgrid/$version")
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives fatJar

  archives jar
  archives javadocJar
  archives sourcesJar
}
